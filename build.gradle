apply from: 'libraries.gradle'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:5.2.1'
    }
}


allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'org.owasp.dependencycheck'

    group = 'org.jpos.ee'
    version = '2.2.8-SNAPSHOT'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    [ compileJava, compileTestJava, javadoc ]*.options*.encoding = 'UTF-8'
    def isSnapshot = version.contains("SNAPSHOT")
    def mavenCentralRepo = isSnapshot ?
        'https://oss.sonatype.org/content/repositories/snapshots/' :
        'https://oss.sonatype.org/service/local/staging/deploy/maven2';

    project.archivesBaseName="jposee-${project.name}"
    javadoc.failOnError = false

    java {
      withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom {
                    name = 'jPOS Project'
                    description = "jPOS Extended Edition"
                    url = "http://jpos.org"
                    organization {
                        name = 'jPOS.org'
                        url = 'http://jpos.org'
                    }
                    issueManagement {
                        system = 'Github Issues'
                        url = 'https://github.com/jpos/jPOS-EE/issues'
                    }
                    scm {
                        url = "http://github.com/jpos/jPOS-EE"
                        connection = "scm:git:https://github.com/jpos/jPOS-EE.git"
                        developerConnection = "scm:git:git@github.com:jpos/jPOS-EE.git"
                    }
                    licenses {
                        license {
                            name = 'GNU AFFERO GENERAL PUBLIC LICENSE'
                            url = 'http://www.gnu.org/licenses/agpl-v3.html'
                            comments = 'See http://jpos.org/license for more details.'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = 'jpos-team'
                            name = 'jPOS Development Team'
                            email = 'info@jpos.org'
                        }
                    }
                }

                groupId = 'org.jpos.ee'
                artifactId = 'jposee'
                from components.java
                artifactId="jposee-${project.name}"

/*
                pom.withXml {
                    asNode().dependencies.'*'.findAll() {
                        it.scope.text() == 'compile' && project.configurations.compile.allDependencies.find { dep ->
                            dep.name == it.artifactId.text()
                        }
                    }.each() {
                        it.scope*.value = 'implementation'
                    }
                    asNode().dependencies.'*'.findAll() {
                        if (it.groupId.text() == 'org.jpos.ee') {
                            it.artifactId*.value = "jposee-${it.artifactId.text()}"
                        }
                    }
                }
*/
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = mavenCentralRepo
                def snapshotsRepoUrl = 'file:///opt/local/maven'
                url = isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
                if (!isSnapshot) {
                    credentials {
                        if (project.hasProperty("mavenCentralUsername"))
                            username = mavenCentralUsername
                        if (project.hasProperty("mavenCentralPassword"))
                            password = mavenCentralPassword
                    }
                }
            }
        }
    } // publishing

    signing {
        required { !isSnapshot }
        sign publishing.publications.mavenJava
    }


    test {
        useJUnitPlatform()
        maxParallelForks = Runtime.runtime.availableProcessors()
    }


    repositories {
        mavenCentral()
        maven { url 'https://jpos.org/maven' }
        maven { url 'https://download.oracle.com/maven' }
        maven { url 'https://maven.vaadin.com/vaadin-addons' }
        maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
        jcenter()
        mavenLocal()
    }

    dependencies {
        testImplementation libraries.junit
        //Java 10 support
        testImplementation('com.sun.xml.bind:jaxb-impl:2.1.2')
        testImplementation('com.sun.activation:javax.activation:1.2.0')
        testImplementation('javax.xml.bind:jaxb-api:2.3.0')
    }

    task allDeps(type: DependencyReportTask) {}
} // subprojects

task allSources(type: Jar,dependsOn: subprojects.assemble) {
    archiveBaseName = 'jposee-sources'
    from subprojects.collect { project ->
        project.sourceSets.main.allSource
    }
}

// Configure IDEA to use Git
idea.project.ipr {
  withXml { provider ->
    provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
  }
}

